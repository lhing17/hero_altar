package Hero

import HashMap
import Event
import ClosureTimers
import GlobalInitialization

HashMap<int, Hero> heroMap = new HashMap<int, Hero>

public function unit.setHero(Hero hero)
    heroMap.put(this.getOwner().getId(), hero)

public function unit.getHero() returns Hero
    return heroMap.get(this.getOwner().getId())

public function player.hasHero() returns boolean
    return heroMap.get(this.getId()) != null

public class Hero
    unit u
    int player_index
    int exp //武学经验
    int potential //潜能
    int dodge //闪避 0-100的整数
    int power //出手
    int skill_level //武艺
    construct(unit u, int player_index)
        this.u = u
        this.player_index = player_index
        this.exp = 0
        this.potential = 0
        this.dodge = 0
        EventListener.add(u, EVENT_UNIT_DEATH, () -> onDeath())

    function onDeath()
        var t = CreateTimer()
        CreateTimerDialogBJ(t,"复活倒计时:")
        TimerDialogDisplayForPlayerBJ(true, bj_lastCreatedTimerDialog, Player(player_index))
        var tg = bj_lastCreatedTimerDialog
        t.start(15, null)
        doAfter(15) ->
            u.revive(HERO_BORN_LOC, true)
            Player(player_index).panCamToTimed(u, 0)
            DestroyTimerDialog(tg)
            t.destr()

    function getExp() returns int
        return this.exp
    
    function setExp(int exp)
        this.exp = exp

    function getPotential() returns int
        return this.potential

    function setPotential(int potential)
        this.potential = potential
    //金钱
    function getMoney() returns int
        return Player(player_index).getGold()
    
    function addMoney(int num)
        Player(player_index).addGold(num)
    //珍稀币
    function getRareMoney() returns int
        return Player(player_index).getLumber()
    
    function addRareMoney(int num)
        Player(player_index).addLumber(num)    
        
    function addLife(real value)
        u.addState(UNIT_STATE_LIFE, value)
    //内力
    function getKi() returns real
        return u.getMaxMana()
    
    
    
    function addPower(int value)
        this.power += value
    
   
    
      
    
     
    